plugins {
    id 'java-library'
    id "com.vanniktech.maven.publish" version "0.29.0"
    id 'signing'
}

group = project.property('projectGroup')
version = project.property('projectVersion')
def projectName = project.property('projectName')
import com.vanniktech.maven.publish.SonatypeHost

mavenPublishing {
    if (version.endsWith("SNAPSHOT")) {
        publishToMavenCentral(
                new SonatypeHost('https://oss.sonatype.org/content/repositories/snapshots/', false)
        )
    } else {
        publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL, true)
    }
    signAllPublications()
    coordinates(group, 'printer', version)
    pom {
        name.set(projectName)
        description.set("A Java Print dependency for easy debugging.")
        url.set("https://github.com/Skylarkarms/Printer")
        licenses {
            license {
                name.set("GNU General Public License v3.0")
                url.set("https://www.gnu.org/licenses/gpl-3.0.en.html")
            }
        }
        developers {
            developer {
                id.set("skylarkarms")
                name.set("Andrew Andrade")
                email.set("juanandrade_20@hotmail.com")
            }
        }
        scm {
            String gitName = projectName.concat(".git")
            connection.set("scm:git:git://github.com/Skylarkarms/".concat(gitName))
            developerConnection.set("scm:git:ssh://git@github.com:Skylarkarms/".concat(gitName))
            url.set("https://github.com/Skylarkarms/".concat(projectName))
        }
    }
}

javadoc {
    options.tags = ['implNote:a:Implementation Note:']
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('io.github.skylarkarms:interfaces:1.0.2')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}